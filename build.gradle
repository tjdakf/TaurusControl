plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

group = 'tauruscontrol'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20250517'
    implementation 'net.java.dev.jna:jna:5.6.0'
    implementation 'net.java.dev.jna:jna-platform:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

javafx {
    version = '21.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainModule = 'tauruscontrol'
    mainClass = 'tauruscontrol.TaurusControlApp'
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

test {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'UTF-8'
}

jlink {
    forceMerge('org.json', 'jna', 'jna-platform')

    mergedModule {
        requires 'java.logging'
        requires 'java.desktop'
        requires 'java.datatransfer'
    }

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'TaurusControl'
        jvmArgs = ['-Dfile.encoding=UTF-8']
    }

    jpackage {
        imageName = 'TaurusControl'
        installerName = 'TaurusControl'
        appVersion = '1.0.0'
        installerType = 'msi'

        imageOptions = [
                '--icon', 'src/main/resources/icon.ico'
        ]

        installerOptions = [
            '--vendor', 'TaurusControl',
            '--description', 'Taurus Series LED Terminal Manager',
            '--win-menu',
            '--win-shortcut',
            '--win-dir-chooser',
            '--icon', 'src/main/resources/icon.ico'
        ]
    }
}

// jpackageImage가 끝난 후 SDK DLL 파일들을 app/native 폴더로 복사
tasks.named('jpackageImage') {
    doLast {
        copy {
            from 'src/main/resources/win32-x86-64'
            into "$buildDir/jpackage/TaurusControl/app/native"
            include '*.dll'
        }
    }
}